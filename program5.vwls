Program 5: Recursive Mergesort
Fnctn nt mrge(flt[] A, nt low, nt mid, nt high) { // Must be flt[] A
    nt i ;
    nt j ;
    nt k ;
    flt T[100] ;   

    i = low ;
    j = mid + 1 ;
    k = low ;

    whl (i <= mid) {
        f (j <= high) {
            f (A[i] <= A[j]) {
                T[k] = A[i] ;
                i = i + 1 ;
            } ls {
                T[k] = A[j] ;
                j = j + 1 ;
            }
        } ls {
            T[k] = A[i] ;
            i = i + 1 ;
        }
        k = k + 1 ;
        f (j > high) {
            whl (i <= mid) {
                T[k] = A[i] ;
                i = i + 1 ;
                k = k + 1 ;
            }
            brk ;
        }
    }

    whl (j <= high) {
        T[k] = A[j] ;
        j = j + 1 ;
        k = k + 1 ;
    }

    i = low ;
    whl (i <= high) {
        A[i] = T[i] ;
        i = i + 1 ;
    }

    rtn 0 ;
}

//Recursive mergesort 
Fnctn nt mrgSrt(flt[] A, nt low, nt high) {
    f (low < high) {
        nt mid ;
        mid = low + (high - low) / 2 ;

        mrgSrt(A, low, mid) ;
        mrgSrt(A, mid + 1, high) ;
        mrge(A, low, mid, high) ;
    }
    rtn 0 ;
}

Fnctn nt main() {
    nt n ;
    nt i ;
    flt A[100] ;
    
    n = scnNT ;

    i = 0 ;
    whl (i < n) {
        A[i] = scnFLT ;
        i = i + 1 ;
    }

    mrgSrt(A, 0, n - 1) ;

    i = 0 ;
    whl (i < n) {
        prnt(A[i]) ;
        i = i + 1 ;
    }

    rtn 0 ;
}